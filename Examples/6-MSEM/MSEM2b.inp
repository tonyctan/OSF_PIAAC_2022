TITLE:	Structural equation modeling with PIAAC data
	Multilevel SEM
	Single-factor model of curiosity and continuous treatment--modified
	Complex design: Total weights, clustering
	Clustered by: country (CNTRYID)
	Level 1--CFA modified
	Level 2--CFA modified, structural model

DATA: 	FILE IS piaac1-all-curiosity.dat;
	! TYPE = IMPUTATION;
	! Not needed here as the model does not contain any PV
		
		
VARIABLE: 
	NAMES ARE 
		CNTRYID
		I_Q04b I_Q04d I_Q04h I_Q04j I_Q04l I_Q04m
		SPFWT0
		HDI2012 UAI;
		
	! Variables to be used
	! Newly defined variable appear at the end of this list
	USEVARIABLES ARE 
		I_Q04b 
		I_Q04d 
		I_Q04h 
		I_Q04j 
		I_Q04l
		I_Q04m
		HDI2012
		UAI;
		
	! Missing data coding
	MISSING ARE ALL(-99);
		
	! Final participant weight
	WEIGHT = SPFWT0;
	
	! Clustering by countries
	CLUSTER = CNTRYID;
	
	! Define between-level-only variables
	BETWEEN = HDI2012 UAI;



ANALYSIS:
	TYPE = TWOLEVEL;
	ESTIMATOR = MLR;
	H1ITERATIONS = 10000;
	
	! Use multiple processors to speed the estimation up
	PROCESSORS = 4;



MODEL:
	%WITHIN%
	! Level 1: Individual participants
	
	! MEASUREMENT MODEL
	! Curiousity
	CURIOUSw BY
		I_Q04b 
		I_Q04d 
		I_Q04h 
		I_Q04j 
		I_Q04l
		I_Q04m;
		
	! Model modifications
	! Residual covariances
	I_Q04b WITH I_Q04h;
	I_Q04l WITH I_Q04j;
	
		
	
	%BETWEEN%
	! Level 2: Countries
	
	! MEASUREMENT MODEL
	! Curiousity
	CURIOUSb BY
		I_Q04b 
		I_Q04d
		I_Q04h
		I_Q04j
		I_Q04l
		I_Q04m;
		
	! Model modifications
	! Residual covariance
	I_Q04L WITH I_Q04J;
	
	
	! STRUCTURAL MODEL
	CURIOUSb ON 
		HDI2012
		UAI;
		
	! Variances of predictors
	HDI2012;
	UAI;
	! Covariance between predictors
	HDI2012 WITH UAI;

	
	
OUTPUT: STDYX;	! Fully standardized parameters requested
        SAMPSTAT;	! Sample statistics 
        CINTERVAL;	! Confidence intervals
        !MOD(all);	! Modification indices