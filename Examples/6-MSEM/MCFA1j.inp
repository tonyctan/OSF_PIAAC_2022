TITLE:	Structural equation modeling with PIAAC data
	Multilevel CFA
	Single-factor model of curiosity and continuous treatment--modified
	Complex design: Total weights, clustering
	Clustered by: country (CNTRYID)
	Level 1--CFA modified
	Level 2--Saturated model, random slope of a factor loading

DATA: 	FILE IS piaac1-all-curiosity.dat;
	! TYPE = IMPUTATION;
	! Not needed here as the model does not contain any PV
		
		
VARIABLE: 
	NAMES ARE 
		CNTRYID
		I_Q04b I_Q04d I_Q04h I_Q04j I_Q04l I_Q04m
		SPFWT0
		HDI2012 UAI;
		
	! Variables to be used
	! Newly defined variable appear at the end of this list
	USEVARIABLES ARE 
		I_Q04b 
		I_Q04d 
		I_Q04h 
		I_Q04j 
		I_Q04l
		I_Q04m;
		
	! Missing data coding
	MISSING ARE ALL(-99);
		
	! Final participant weight
	! WEIGHT = SPFWT0;
	! Not possible with Bayes estimation in Mplus
	
	! Clustering by countries
	CLUSTER = CNTRYID;


ANALYSIS:
	TYPE = TWOLEVEL RANDOM;
	ESTIMATOR = BAYES;
	FBITERATIONS = 10000;
	
	! Use multiple processors to speed the estimation up
	PROCESSORS = 4;



MODEL:
	%WITHIN%
	! Level 1: Individual participants
	
	! MEASUREMENT MODEL
	! Curiousity
	CURIOUSw BY
		I_Q04b 
		I_Q04d 
		I_Q04h  
		I_Q04l
		I_Q04m;
		
	! Model modifications
	! Residual covariances
	I_Q04b WITH I_Q04h;
	I_Q04l WITH I_Q04j;
	
	! Random slope of a factor loading
	s | CURIOUSw BY I_Q04j;
	
		
	
	%BETWEEN%
	! Level 2: Countries
	
	! Saturated model
	! Intercepts
	[I_Q04b-I_Q04m];
	[s];
	! Variances estimated
	I_Q04b-I_Q04m;
	s;
	! Covariances
	I_Q04b-I_Q04m WITH I_Q04b-I_Q04m;
	s WITH I_Q04b-I_Q04m;

	
	
OUTPUT: STDYX;	! Fully standardized parameters requested
        SAMPSTAT;	! Sample statistics 
        CINTERVAL;	! Confidence intervals
        !MOD(all);	! Modification indices